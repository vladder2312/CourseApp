apply from: '../android-standard/androidStandardConfig.gradle'
import groovy.json.JsonSlurper
// Скрипт для включения модулей android-standard из локального репозитория в settings.gradle

if (gradle.ext.androidStandard.debugEnabled) {
    try {
        def androidStandardDebugDir = gradle.ext.androidStandard.debugDir
        def components = new JsonSlurper().parse(new File("$androidStandardDebugDir/buildSrc/components.json"), 'utf-8')
        assert components instanceof List
        includeBuild("$androidStandardDebugDir") {
            dependencySubstitution {
                components.each { component ->
                    if (component.libraries != null && !component.libraries.isEmpty()) {
                        component.libraries.each { library ->
                            substitute module("ru.surfstudio.android:${library.artifactName}") with project(":${library.artifactName}")
                        }
                    }
                }
            }
        }
    } catch (FileNotFoundException ignored) {
        logger.error("file buildSrc/components.json  in local android standard folder  not found")
    }
}